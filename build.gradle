import groovy.json.JsonSlurper

plugins {
  id 'java'
  id 'org.springframework.boot' version '1.4.3.RELEASE'
  id "org.hidetake.swagger.generator" version "2.4.0"

  id "com.dorongold.task-tree" version "1.2.2" // taskTree
}

repositories {
  jcenter()
}

dependencies {
  // Add dependency for Swagger Codegen CLI
  swaggerCodegen 'io.swagger:swagger-codegen-cli:2.2.1'
}

ext.swagger = [
  inputFile: file('api.swagger.yml'),
  configFile: file('swagger-config.json')
        ]
// derived values
ext.swagger.config = (new JsonSlurper()).parse(ext.swagger.configFile)
ext.swagger.outputDir = "${project.buildDir}/${ext.swagger.config.sourceFolder}"


sourceSets {
  generated {
    java.srcDir file(swagger.outputDir)
  }

  main {
    java {
      compileClasspath += sourceSets.generated.output
    }
  }
}

clean {
  delete (sourceSets.generated.allSource)
}

validateSwagger {
  inputFile = swagger.inputFile
}

generateSwaggerCode {
  language = 'spring'
  inputFile = swagger.inputFile
  configFile = swagger.configFile
  components = ['models', 'apis']

  inputs.files(inputFile, configFile)
  outputs.dir(outputDir)
}

compileGeneratedJava {
  inputs.files(generateSwaggerCode.outputs)
}

compileJava {
  inputs.files(compileGeneratedJava.outputs)
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
  compile {
    extendsFrom generatedCompile
  }
}

jar {
  from sourceSets.generated.output
}

dependencies {
  generatedCompile 'org.springframework.boot:spring-boot-starter-web'
  generatedCompile 'io.swagger:swagger-annotations:1.5.12'

  compile 'org.springframework.boot:spring-boot-starter-web'

  testCompile('org.springframework.boot:spring-boot-starter-test')
}

bootRepackage {
  mainClass = 'org.mohme.esj.Application'
}

task elmMake(type: ElmMake) {
  sourceDir = 'src/main/elm'
  debug = false

  inputs.dir(sourceDir)
}
jar.dependsOn elmMake


class ElmMake extends DefaultTask {
  private final String elm_make  =   "elm-make";

  @Input sourceDir = 'src/main/elm'
  @Input buildDir = "${project.buildDir}/elm"
  @Input mainModule = 'Main.elm'
  @Input targetModule = 'elm.js'
  @Input debug = true

  @TaskAction
  def build(){
    createBuildDirectory(buildDir);
    String[] elm_make_cmd = [elm_make, "${sourceDir}/${mainModule}", "--output", "${buildDir}/${targetModule}" ]
    if (debug) {
      elm_make_cmd += '--debug'
    };
    buildElmProject(elm_make_cmd)
  }

  def createBuildDirectory(String build_dir){
    if(! new File(build_dir).exists())
      new File(build_dir).mkdirs()
  }

  def buildElmProject(String[] make_cmd){
    logger.quiet "executing ${make_cmd}"
    ProcessBuilder pBuilder = new ProcessBuilder(make_cmd);
    pBuilder.redirectErrorStream(true);
    Process process = pBuilder.start();

    def line;
    def reader = new BufferedReader(new InputStreamReader(process.getInputStream()));

    while ((line = reader.readLine()) != null) {
      logger.info line
    }
  }

}